/*******************************************************************************
*         McStas instrument definition URL=http://www.mcstas.org
*
* Instrument: NARI monochromator Diffractometer
*
* %Identification
* Written by: Chen LiFang
* Date: 5 July 2024
* Origin: NARI
* %INSTRUMENT_SITE: NARI
*
* Simple monochromator Diffractometer for powders.
*
* %Description
* Monochromator           
* HOPG 002 DM=3.35 AA
* Ge 111 DM=3.26 AA
* Ge 115 DM=1.0925 AA     
* Ge 113 DM=1.712 AA    
*
* %Parameters
* I0：[/s]                    Intensity of neutron source.
* lambda: [Angs]              Target Wavelength at sample stage. 
* Beta: [minutes of arc]      Mosaic spread of Monochromator.
* Mono_d: [Angs]              d-spacing of monochromator.
* Focus_H:                    Focus function in Horizontal direction. 0/1：closed/opened
* Focus_V：                   Focus function in Vertical direction. 0/1：closed/opened
* Co1: [minutes of arc]       Divergence horizontal angle of collimator 1.
* Co2: [minutes of arc]       Divergence horizontal angle of collimator 2.
* Co3: [minutes of arc]       Divergence horizontal angle of collimator 3.
* L_MS：[m]                   The distance between monochromator and sample stage.
* L_MSlit3:[m]                The distance between monochromator and Slit3.
* string sample: [str]        File name for powder sample description.
* detector_bins:              The range of angle of Banana Detector is 157 degrees. 
*                             The coverage angle of a single detector is 157/detector_bins degrees.
* detector_height:[m]         Height of detector.
* m1:                         m-value of guide1.
* m2:                         m-value of guide2.        
*
* %End
*******************************************************************************/


DEFINE INSTRUMENT NARIDiff (I0=3.64e+014,Lambda=1.65,Beta=30,Mono_d=3.35,  Focus_H=0, Focus_V=0,Co1=30,Co2=30,Co3=30,L_MS=2,L_MSlit3=1.95,string sample="Ag.laz",detector_bins=314,detector_height=0.2,m1=3,m2=1)

/* Version of 20240331  By LiFangChen*/

DECLARE

%{
  double Ki, Co1,Co2 ,Co3,Ein, dEin, sina1, A1, A2, RVN, RHN, L0, L_MS, L_MSlit3,LVS, I0, Lambda,Mono_d, Mono_reflect, Q0,Beta,pixel,detector_height,m1,m2;
  double flag_container,flag_sample,flag_env;
  double verbose=1;
  double Focus_H, Focus_V;
  int detector_bins;
  char str1[60]={"banana, theta limits=[-160 -3], bins="};
  char str2[6];
  char sample1[60];
%}

INITIALIZE

/* E=p^2/2m = (hbar^2)*(Ki^2)/2m =2.07212*Ki^2 p=k * hbar = 1.054573 E -34 * k; m=1.67493 e-27 
   1 meV = 1.60218 e-22 */
%{
#include <stdio.h>
#include <string.h>

Ki = 2*PI/Lambda;
Ein=2.07212*Ki*Ki;
dEin=Ein*0.10;
/*Lc=2*PI/Ki;*/
sina1= PI/Mono_d/Ki;
A1=180.0*asin(sina1)/PI;
A2=2.0*A1;
pixel = (160.0-3.0)/detector_bins;

itoa( detector_bins, str2, 10);
strcat(str1, str2);


/*  RVN   formula corresponds to focusing at source  */
/*  RHN   formula corresponds to focusing at virtual source  */
/*  L_MS  monochromator-sample distance  */
/*  LVS   virtual source - monochromator  */

L0=7; /*source to Guide End*/
LVS=0.5;

if (Focus_H == 1)
    {RHN=2.0*(LVS+L0)*L_MS/sina1/(LVS+L0+L_MS);}

if (Focus_V == 1)
    {RVN=2.0*sina1*(LVS+L0)*L_MS/(LVS+L0+L_MS);}


Q0 = 2*PI/Mono_d;

printf("A1 (deg) A1 = %f\n",A1);
printf("A2 (deg) A2 = %f\n",A2);
printf("Ki = %f\n",Ki); 
printf("Ei = %f\n",Ein);
printf("Q0 = %f\n",Q0); 
printf("Lambda = %f\n",Lambda); 
printf("L0 = %f m\n",L0); 
printf("m1 = %f \n",m1);
printf("m2 = %f \n",m2);
printf("Beta = %f\n",Beta); 
printf("Mono_d = %f\n" ,Mono_d); 
printf("RH = %f\n",RHN); 
printf("RV = %f\n",RVN);
printf("L_MS = %f\n",L_MS);
printf("Co1 = %f\n",Co1);
printf("Co2 = %f\n",Co2);
printf("Co3 = %f\n",Co3);
printf("pixel = %f degrees\n",pixel);
printf("sample = %s\n",sample);
printf("detector = %s\n",str1);
%}

TRACE

COMPONENT src = Arm()  AT (0,0,0) ABSOLUTE

/* souce parameter */
/*Maxwell Boltzman distribution, T_peak=330.43*/
COMPONENT source1 = Source_gen(
                               yheight  = 0.12,
                               xwidth   = 0.06,
                               dist     = 0.02,
                               focus_aw = 1,
                               focus_ah = 1,
                               E0  = 200,
                               dE  = 199,
                               T1=330.43,I1=I0,
                               verbose  = 1) 
AT (0,0,0) RELATIVE src

/*E_Monitor_1*/
COMPONENT E_Monitor_1 = E_monitor(
                          xmin=-0.30,
                          xmax=0.30,
                          ymin=-0.30,
                          ymax=0.30,
                          nE=200,
                          filename="E_Monitor_1.txt",
                          Emin=0,
                          Emax=200)
AT(0,0,0.00011) RELATIVE src

/*COMPONENT PSD_1 = PSD_monitor(
                          xmin = -0.30, 
                          xmax = 0.30,
                          ymin = -0.30, 
                          ymax = 0.30,
                          nx = 120, 
                          ny = 120,
                          filename = "VS_XY.psd")
AT(0,0,0.00012) RELATIVE src*/


/*Slit 1*/
COMPONENT Slit1=Slit(
                          xmin=-0.03,
                          xmax=0.03,
                          ymin=-0.06,
                          ymax=0.06)
AT (0,0,0.0009) RELATIVE src

/*  Super Mirror Guide  */
COMPONENT guide1=Guide(w1 = 0.06, 
                       h1 = 0.12, 
                       w2 = 0.06, 
                       h2 = 0.12, 
                       l = L0,
                       R0=0.99,
                       m=m1)
AT (0,0,0.001) RELATIVE src

/*Slit 2*/
COMPONENT Slit2=Slit(
                          xmin=-0.03,
                          xmax=0.03,
                          ymin=-0.06,
                          ymax=0.06)
AT (0,0,L0+0.001) RELATIVE src

/*E_Monitor_2*/
COMPONENT E_Monitor_2 = E_monitor(
                          xmin=-0.30,
                          xmax=0.30,
                          ymin=-0.30,
                          ymax=0.30,
                          nE=200,
                          filename="E_Monitor_2.txt",
                          Emin=0,
                          Emax=200)
AT(0,0,L0+0.0011) RELATIVE src

COMPONENT PSD_2 = PSD_monitor(
                          xmin = -0.30, 
                          xmax = 0.30,
                          ymin = -0.30, 
                          ymax = 0.30,
                          nx = 120, 
                          ny = 120,
                          filename = "VS_XY.psd")
AT(0,0,L0+0.0011) RELATIVE src


/* Collimator No1 */
COMPONENT Collimator_No1= Collimator_linear(
                          xmin= -0.05, 
                          xmax= 0.05, 
                          ymin= -0.08,
                          ymax= 0.08, 
                          length= 0.15, 
                          divergence= Co1,
                          transmission=0.7)
AT(0,0,L0+0.05) RELATIVE src 

/*  Q = 2Pi/d = 2Pi/PG(002) = 2Pi/3.355 = 1.872783  */

/*Monochromator*/
COMPONENT Mono_Cradle = Arm()
AT (0, 0, L0+LVS) RELATIVE src ROTATED (0, A1, 0) RELATIVE src

COMPONENT graphit_mono = Monochromator_curved(
                          zwidth=0.02,
                          yheight=0.03,
                          gap=0.0005,
                          NH=5,
                          NV=5,
                          mosaich=Beta,
                          mosaicv=Beta,
                          reflect="HOPG.rfl",
                          Q=Q0,
                          RV=RVN,
                          RH=RHN)
AT (0,0,0.00001) RELATIVE Mono_Cradle 

/* Monitor at beamstop */
/*COMPONENT Beamstop1 =  Beamstop(xmin=-0.050, 
                                  xmax=0.05, 
                                  ymin=-0.1, 
                                  ymax=0.1)
AT(0,0,LVS+0.5) RELATIVE src

COMPONENT PSD_Beamstop1 = PSD_monitor(
                          xmin = -0.20, 
                          xmax = 0.20,
                          ymin = -0.30, 
                          ymax = 0.30,
                          nx = 120, 
                          ny = 120,
                          filename = "Beamstop_XY_1.psd")
AT(0,0,LVS+0.51) RELATIVE src*/


COMPONENT Mono_Out = Arm()      
AT (0,0,0.00002) RELATIVE Mono_Cradle ROTATED (0, A2, 0) RELATIVE src

COMPONENT guide2=Guide(w1 = 0.07, 
                       h1 = 0.14, 
                       w2 = 0.02, 
                       h2 = 0.03, 
                       l = L_MSlit3-0.15,
                       R0=0.99,
                       m=m2)
AT (0,0,0.15) RELATIVE Mono_Out

/*Filter_HOPG*/
COMPONENT Filter_HOPG =Filter_gen(
                           xwidth=.2 ,
                           yheight=.3, 
                           thickness=3.0,
                           filename="HOPG.trm")
AT (0,0,0.5) RELATIVE Mono_Out


/* Collimator No2 */
COMPONENT Collimator_No2= Collimator_linear(
                          xmin= -0.035, 
                          xmax= 0.035, 
                          ymin= -0.045,
                          ymax= 0.045, 
                          length= 0.15, 
                          divergence= Co2,
                          transmission=0.7)
AT(0,0,0.7) RELATIVE Mono_Out

/*Slit 3*/
COMPONENT Slit3=Slit(
                          xmin=-0.01,
                          xmax=0.01,
                          ymin=-0.015,
                          ymax=0.015)
AT (0,0,L_MSlit3) RELATIVE Mono_Out

/*E_Monitor_sample_3*/
COMPONENT E_Monitor_3 = E_monitor(
                          xmin=-0.30,
                          xmax=0.30,
                          ymin=-0.30,
                          ymax=0.30,
                          nE=2000,
                          filename="E_Monitor_sample_3.txt",
                          Emin=0,
                          Emax=200)
AT(0,0,L_MS-0.002) RELATIVE  Mono_Out

COMPONENT PSD_Sample = PSD_monitor(
                          xmin = -0.12, 
                          xmax = 0.12,
                          ymin = -0.12, 
                          ymax = 0.12,
                          nx = 120, 
                          ny = 120,
                          filename = "Sample_XY.psd")
AT(0,0,L_MS-0.001) RELATIVE Mono_Out


/* sample position ********************************************************** */


SPLIT COMPONENT SamplePos=Arm()
AT (0, 0, L_MS) RELATIVE Mono_Out
EXTEND %{
  flag_container=flag_sample=flag_env=0;
%}


COMPONENT Sample=PowderN(reflections = sample,
                         radius = 0.008/2, yheight = 0.03,
                         d_phi=RAD2DEG*atan2(0.5,0.5), p_transmit=0.08, p_inc=0.05)
AT (0, 0, 0) RELATIVE SamplePos
EXTEND %{
  if (SCATTERED)
    flag_sample=SCATTERED;
%}

/* sample position (end) **************************************************** */

/* Collimator No3 */
COMPONENT Collimator_No3=Collimator_radial(xwidth=0, yheight=.25, length=.2,
                      divergence=Co3,transmission=1, nchan=32,
                      theta_min=-160, theta_max=0, radius=0.75)
AT (0, 0, 0) RELATIVE SamplePos

EXTEND %{
  if (!flag_sample && !flag_container) ABSORB;
%}

/* perfect detector: 1D(theta) */
COMPONENT BananaTheta = Monitor_nD(
    options = str1,
    xwidth = 1.0*2, yheight = detector_height)
  AT (0, 0, 0) RELATIVE SamplePos

/* perfect detector: 2D(theta,y) to see diffraction rings */
COMPONENT BananaPSD = Monitor_nD(
    options = "banana, theta limits=[-160 -5] bins=380, y bins=25",
    xwidth = 1.0*2*1.005, yheight = 1.0)
  AT (0, 0, 0) RELATIVE SamplePos


COMPONENT Beamstop2 =  Beamstop(xmin=-0.05, 
                                  xmax=0.05, 
                                  ymin=-0.1, 
                                  ymax=0.1)
AT(0,0,1.5) RELATIVE SamplePos

COMPONENT PSD_Beamstop2 = PSD_monitor(
                          xmin = -0.20, 
                          xmax = 0.20,
                          ymin = -0.30, 
                          ymax = 0.30,
                          nx = 120, 
                          ny = 120,
                          filename = "Beamstop_XY_2.psd")
AT(0,0,1.51) RELATIVE SamplePos 


END

